apply plugin: 'com.android.application'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        applicationId "me.kuehle.carreport"
        minSdkVersion 15
        targetSdkVersion versions.targetSdk
        versionCode 64
        versionName "3.18.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias signing_key_alias
            keyPassword signing_key_password
            storeFile file(signing_key_store_file)
            storePassword signing_key_store_password
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions "tier"
    productFlavors {
        foss {
            dimension "tier"
        }
        full {
            dimension "tier"
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    google()
    jcenter()

    // Required for HelloCharts for Android
    maven { url "https://jitpack.io" }
}

dependencies {
    // Charts
    implementation 'com.github.gudatcomputers:hellocharts-android:ba603174d302931c0985afb4e54d597fba28146b'

    // CSV
    implementation 'org.apache.commons:commons-csv:1.5'

    // Dropbox
    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.5'

    // Support, AppCompat
    implementation "com.android.support:support-v4:${versions.supportLibrary}"
    implementation "com.android.support:appcompat-v7:${versions.supportLibrary}"
    implementation "com.android.support:cardview-v7:${versions.supportLibrary}"
    implementation "com.android.support:recyclerview-v7:${versions.supportLibrary}"
    implementation "com.android.support:design:${versions.supportLibrary}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Play Services: for Google Drive
    fullCompile "com.google.android.gms:play-services-auth:${versions.playServices}"
    fullCompile "com.google.android.gms:play-services-drive:${versions.playServices}"

    // The new Android Drive API, which is included in the play services, has caching bugs and does
    // not provide all functionality. Until this is fixed we have to use the old Google API.
    fullImplementation('com.google.oauth-client:google-oauth-client:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.api-client:google-api-client:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.apis:google-api-services-drive:v3-rev81-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.http-client:google-http-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    // WebDAV
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'

    // UI Helpers
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.2'
    implementation 'uk.co.jordanrobinson:android-color-picker:1.0.2'

    // Joda-Time for Android
    implementation 'net.danlew:android.joda:2.9.9'

    // Required for instrumented tests
    androidTestImplementation "com.android.support:support-annotations:${versions.supportLibrary}"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}